#!/bin/zsh

# Convert '.zshrc' aliases starting with 'cd' or 'nvim' and make into Vim
# mappings, and take '.zshrc' shell variables and make into Vim variables.
# Write results to 'zshrc.vim', to be sourced in 'init.vim'.

setopt rc_quotes
setopt extended_glob

echo '" Mappings (corresponding to aliases in ''.zshrc'')\n' \
    > $XDG_CONFIG_HOME/nvim/zshrc.vim
while read line; do
    if [[ $line[1,5] != 'alias' ]]; then
        continue
    fi

    if [[ ! $line =~ '=(''|")?(cd |nvim )' ]]; then
        continue
    fi

    lhs=${line%%=*}
    lhs=${lhs##*[[:space:]]}
    rhs=${line#*=}
    rhs=${${rhs#(\'|\")}%(\'|\")}
    map="nnoremap <Leader>$lhs <Cmd>$rhs<CR>"
    map=${map/'<Cmd>cd'/'<Cmd>lcd\n            \'}
    map=${map/'<Cmd>nvim'/'<Cmd>vs\n            \'}
    echo $map
done < $XDG_CONFIG_HOME/zsh/.zshrc >> $XDG_CONFIG_HOME/nvim/zshrc.vim

echo '\n" Variables (corresponding to shell variables in ''.zshrc'')\n' \
    >> $XDG_CONFIG_HOME/nvim/zshrc.vim
while read line; do
    if [[ ! $line =~ '^[[:lower:][:digit:]]+=' ]]; then
        continue
    fi

    name=${line%%=*}
    value=${line#*=}
    echo 'let' $name '=' $value
done < $XDG_CONFIG_HOME/zsh/.zshrc >> $XDG_CONFIG_HOME/nvim/zshrc.vim
