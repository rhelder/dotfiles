#!/bin/zsh

# Construct a Vimscript file that defines mappings based on '.zshrc' aliases
# and variables based on '.zshrc' shell variables. Use aliases beginning with
# 'cs' or 'nvim' to create mappings useful for navigation or editing. Include
# only variables that contain file or directory pathnames.

setopt RC_QUOTES
setopt EXTENDED_GLOB

declare -a maps
declare -a vars
blacklist=( 'HIST' 'PS1' 'rfv' )

while read line; do
  if [[
    $line[1,5] = 'alias'
    && $line =~ '=(''|")?(cs |cd | nvim )'
  ]]; then
  lhs=${line%%=*}
  lhs=${lhs#alias }
  rhs=${line#*=}
  rhs=${${rhs#(\'|\")}%(\'|\")}
  map="nnoremap <Leader>$lhs <Cmd>$rhs<CR>"
  map=${map/'<Cmd>cs'/'<Cmd>lcd'}
  map=${map/'<Cmd>cd'/'<Cmd>lcd'}
  map=${map/'<Cmd>nvim'/'<Cmd>vs'}
  maps+=$map
elif [[ $line =~ '^[[:alnum:]_]+=' ]]; then
  name=${line%%=*}
  value=${line#*=}
  vars+="let $name = $value"
  fi
done < $XDG_CONFIG_HOME/zsh/.zshrc

for pattern in $blacklist; do
  vars=( ${vars:#*${~pattern}*} )
done

echo '" Mappings (corresponding to aliases in ''.zshrc'')\n' \
    > $XDG_CONFIG_HOME/nvim/zshrc.vim
print -l $maps >> $XDG_CONFIG_HOME/nvim/zshrc.vim

echo '\n" Variables (corresponding to shell variables in ''.zshrc'')\n' \
    >> $XDG_CONFIG_HOME/nvim/zshrc.vim
print -l $vars >> $XDG_CONFIG_HOME/nvim/zshrc.vim
