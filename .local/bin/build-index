#!/bin/zsh

# Get keywords from the YAML metadata blocks of all Markdown files in
# $HOME/Documents/Notes. For each keyword, build an index file containing links
# to all the files that have that keyword, including other index files.
# Hierarchically organize the file so that links to other index files with that
# keyword are formatted as headings, and so that links to files containing both
# that keyword and the keyword corresponding to the other index file are listed
# under that heading. Then list links to any stragglers at the end.

# Define key_regex {{{1

setopt rc_quotes

declare key_start_chars='[^\-?:,[\]{}#&*!|>''"%@`[:space:]]'
declare key_can_start_if_chars='[?:\-](?=[^[:space:]])'
declare key_chars="(${key_start_chars}|${key_can_start_if_chars})"
declare key_subsequent_chars='(\S#|:\S|[^[:space:]:#])*'
key_chars="${key_chars}${key_subsequent_chars}"

declare unquoted_key_regex="${key_chars}(\s+${key_chars})*"
declare quoted_key_regex="'([^']|'')*'"
declare dquoted_key_regex='"([^"]|\\")*(?<!\\)"'
declare key_regex="^\s*(?(?=^')${quoted_key_regex}|"
key_regex="${key_regex}(?(?=^\")${dquoted_key_regex}|"
key_regex="${key_regex}${unquoted_key_regex}))\s*:(\s|$)"

_get_yaml_block() { # {{{1
    local -a start_end_line_numbers=(
        ${(f)"$(
            pcre2grep --line-number '^(---|\.\.\.)\s*$' $1 | cut -d : -f 1
        )"}
    )

    if (( $#start_end_line_numbers < 2 )); then
        echo 'build-index: cannot get YAML block for' $1 >&2
        return 1
    fi

    local -a key_line_numbers=(
        ${(f)"$(
            pcre2grep --line-number $key_regex $1 | cut -d : -f 1
        )"}
    )

    local -a file=( "${(@f)$(cat $1)}" )
    local -a block=()

    while __start_found $start_end_line_numbers; do
        local -i block_start=$start_end_line_numbers[1]
        local -i block_end=$start_end_line_numbers[2]
        if (( block_end - block_start == 1 )); then
            [[ ! -n $got_empty_block ]] && local -i got_empty_block=1
        else
            block+=( $file[block_start+1,block_end-1] )
        fi

        # Remove the starting and ending line numbers from the array
        start_end_line_numbers=( $start_end_line_numbers[3,-1] )
        if [[ ! -n $start_end_line_numbers ]]; then
            break
        fi
    done

    if [[ ! -n $block && ! -n $got_empty_block ]]; then
        echo 'build-index: cannot get YAML block for' $1 >&2
        return 1
    fi

    # Cache the block(s) in the `blocks` associative array
    blocks[$1]=${(j:++:)block}
}

__start_found() { # {{{2
    # Return 1 if the start of a YAML block is found; return 0 if it is not
    # found. Takes the array `start_end_line_numbers` as its argument
    while true; do
        # The start must be a `---` line (as opposed to a `...` line); it must
        # not be followed by a blank line; and, if it is not the first line of
        # the document, it must be preceded by a blank line. Eliminate any
        # lines that do not meet these criteria until a possible start is
        # found.
        while [[
            $file[$1] != ---[[:space:]]# ||
            $file[$1+1] == [[:space:]]# ||
            ($1 -ne 1 && $file[$1-1] != [[:space:]]#)
        ]]; do
            shift

            if (( $# < 2 )); then
                start_end_line_numbers=( $* )
                return 1
            fi
        done

        start_end_line_numbers=( $* )
        local -i possible_start=$start_end_line_numbers[1]
        local -i possible_end=$start_end_line_numbers[2]

        # Check if block is empty
        (( possible_end - possible_start == 1 )) && return 0

        # Check if there is a key
        if [[ -n $key_line_numbers ]] && __key_found $key_line_numbers; then
            start_end_line_numbers=( $* )
            return 0
        else
            shift
            continue
        fi
    done
}

__key_found() { # {{{2
    # Return 0 if a key is found in the possible YAML block; return 1 if one is
    # not found. Takes `key_line_numbers` as its argument
    while (( $1 < possible_start )); do
        shift

        if (( $# == 0 )); then
            key_line_numbers=( $* )
            return 1
        fi
    done

    if (( $1 > possible_start && $1 < possible_end )); then
        key_line_numbers=( $* )
        return 0
    else
        key_line_numbers=( $* )
        return 1
    fi
}
# }}}2

_parse_yaml_block() { # {{{1
    # Get values from first line {{{2

    # Find the line in the block with the key we're looking for; disregard
    # any lines before this
    local -a block=( ${(s:+:)blocks[$2]} )
    local -i first_line_num=$block[(I)[[:space:]]#${1}:([[:space:]]##*)#]

    if (( $first_line_num > $#block )); then
        echo 'build-index: for key' $1 'in' $2',' \
            'key not found' >&2
        return 1
    fi

    # Get indent of the line containing the key; every subsequent line is going
    # to have to be indented at least as much as this line, unless it's part of
    # a properly quoted or bracketed value
    local -i key_indent=$(__get_indent $block[$first_line_num] )

    # Get value (if there is one) and trim whitespace from both ends
    local value=${block[$first_line_num]##[[:space:]]#${1}:}
    value=${${value##[[:space:]]#}%%[[:space:]]#}
    local -a values
    values+=( $value )

    # If there is a value on the first line, do not parse subsequent lines
    # beginning with a '- ' as list items
    if [[ -n $value ]]; then
        local -i no_list=1
    else
        local -i no_list=0
    fi

    local -i single_quote=0
    local -i double_quote=0
    local -i bracket=0
    __check_quotes

    # Get values from subsequent lines {{{2

    local -a block_slice=( $block[(( $first_line_num + 1 )),-1] )
    local line
    for line in $block_slice; do
        local -i line_indent=$(__get_indent $line)

        # Trim whitespace from both ends
        value=${${line##[[:space:]]#}%%[[:space:]]#}

        if [[
            no_list -eq 0 &&
            single_quote -eq 0 &&
            double_quote -eq 0 &&
            bracket -eq 0 &&
            $value[1,2] == '- '
        ]]; then

            if [[ $1 == title ]]; then
                echo 'build-index: for key' $1 'in' $2',' \
                    'list not allowed' >&2
                return 1

            elif (( line_indent < key_indent )); then
                echo 'build-index: for key' $1 'in' $2',' \
                    'line outside scope of key' >&2
                return 1

            elif (( line_indent >= key_indent )); then
                # If `list_indent` has not been defined, then this is the first
                # list item. Therefore, set `list_indent` to whatever the
                # `line_indent` of this line is.
                if [[ ! -n $list_indent ]]; then
                    local -i list_indent=$line_indent
                fi

                if (( line_indent < list_indent )); then
                    echo 'build-index: for key' $1 'in' $2',' \
                        'line outside scope of list' >&2
                    return 1

                elif (( line_indent >= list_indent )); then
                    value=${value##-[[:space:]]##}

                    if [[ $value[1] == '[' ]]; then
                        echo 'build-index: for key' $1 'in' $2',' \
                            'list not allowed within list' >&2
                        return 1
                    fi

                    values+=( $value )
                    __check_quotes
                fi
            fi

        else
            # If the string is quoted or bracketed, YAML disregards normal
            # indentation rules and always appends subsequent lines to the
            # current value
            if ((
                single_quote == 1 ||
                double_quote == 1 ||
                bracket == 1
            )); then
                __append_to_string || return 1

            elif (( line_indent < key_indent )); then
                echo 'build-index: for key' $1 'in' $2',' \
                    'line outside scope of key' >&2
                return 1

            elif (( line_indent == key_indent )); then
                # If the line has the same indentation as the key, then it
                # either has to be a list item beginning with '- ', which we've
                # already handled, or another key/value pair (in which case
                # we're done parsing)
                if echo $value | pcre2grep $key_regex >/dev/null; then
                    break
                else
                    echo 'build-index: for key' $1 'in' $2',' \
                        'key/value pair expected' >&2
                    return 1
                fi

            elif (( line_indent > key_indent )); then
                if [[ -n $list_indent ]]; then
                    if (( line_indent > list_indent )); then
                        __append_to_string || return 1
                    else
                        echo 'build-index: for key' $1 'in' $2',' \
                            'line outside scope of list' >&2
                        return 1
                    fi

                else
                    __append_to_string || return 1
                    # If ever there is an indented line with a value before
                    # there is a list, no list can follow
                    no_list=1
                fi
            fi
        fi

        __check_quotes
    done

# Process values {{{2

    # Process values with brackets {{{3

    if [[ ${values[1][1]} == '[' ]]; then
        if [[ $1 == 'title' ]]; then
            echo 'build-index: for key' $1 'in' $2',' \
                'list not allowed' >&2
            return 1

        elif [[ ${values[-1][-1]} == ']' ]]; then
            value=${${values#'['}%']'}

        else
            echo 'build-index: for key' $1 'in' $2',' \
                'closing bracket not found' >&2
            return 1
        fi

        values=( ${(s:,:)value} )
        local -i index=1
        for value in $values; do
            value=${${value##[[:space:]]#}%%[[:space:]]#}

            if [[ $value[1,2] == '- ' ]]; then
                echo 'build-index: for key' $1 'in' $2',' \
                    'list not allowed within list' >&2
                return 1
            fi

            values[$index]=$value
            index=$(( index + 1 ))
        done
    fi

    # Process values with quotes {{{3
    # Given the way that values have been collected, any value that starts with
    # a quote but does not end with a quote is breaking YAML parsing rules. If
    # there is both a starting and ending quote, strip off the outside quotes
    # and substitute properly escaped interior quotes with '++'. Then, if any
    # quotes remain, that means that there are unescaped quotes.

    local -i index=1
    for value in $values; do
        if [[ $value[1] == "'" ]]; then
            if [[ $value[-1] == "'" ]]; then
                value=${${value#\'}%\'}
                value=${value//\'\'/++}

                if [[ $value =~ "'" ]]; then
                    echo 'build-index: for key' $1 'in' $2',' \
                        'unescaped single quotes found' >&2
                    return 1
                fi

                value=${value//++/\'}
                values[$index]=$value

            else
                echo 'build-index: for key' $1 'in' $2',' \
                    'closing quote not found' >&2
                return 1
            fi

        elif [[ $value[1] == '"' ]]; then
            value=${value//\\\"/++}
            if [[ $value[-1] == '"' ]]; then
                value=${${value#\"}%\"}

                if [[ $value =~ '"' ]]; then
                    echo 'build-index: for key' $1 'in' $2',' \
                        'unescaped double quotes found' >&2
                    return 1
                fi

                value=${value//++/\"}
                values[$index]=$value

            else
                echo 'build-index: for key' $1 'in' $2',' \
                    'closing quote not found' >&2
                return 1
            fi

        else
            # If the value isn't quoted, it can't start with a '@' or
            # contain a ':'
            if [[ $value[1] == '@' ]]; then
                echo 'build-index: for key' $1 'in' $2',' \
                    "value begins with '@'" >&2
                return 1
            elif [[ $value =~ ':' ]]; then
                echo 'build-index: for key' $1 'in' $2',' \
                    'value contains nested key/value pair' >&2
                return 1
            fi
        fi

        index=$(( index + 1 ))
    done
    # }}}3

    # Split keywords one more time at commas, like Pandoc's YAML parser does
    if [[ $1 == 'keywords' ]]; then
        values=( ${(s:,:)${(j:,:)values}} )
        index=1
        for value in $values; do
            value=${${value##[[:space:]]#}%%[[:space:]]#}
            values[$index]=$value
            index=$(( index + 1 ))
        done
    fi

    print -l -- ${values//[[:space:]]##/ }
# }}}2
}

__check_quotes() { # {{{2
    if [[ $value[1] == "'" ]]; then
        single_quote=1
    elif [[ $value[1] == '"' ]]; then
        double_quote=1
    elif [[ $value[1] == '[' ]]; then
        bracket=1
    fi

    if [[ $value[-1] == "'" && $value[-2] == [^\'] ]]; then
        single_quote=0
    elif [[ $value[-1] == '"' && $value[-2] == [^\\] ]]; then
        double_quote=0
    elif [[ $value[-1] == "]" ]]; then
        bracket=0
    fi
}

__get_indent() { # {{{2
    echo ${#${1}%%[^[:space:]]*}
}

__append_to_string() { # {{{2
    if [[ -n $values[-1] ]]; then
        values[-1]="$values[-1] $value"
    else
        values[-1]=$value
    fi
}
# }}}2

# }}}1

# Build an index file for every keyword found in notes {{{1

setopt extended_glob
setopt typeset_silent

if [[ $1 == (--pandoc-always|-p) ]]; then
    integer pandoc_always=1
    shift
fi

cd $HOME/Documents/Notes || exit 1

# Cache keywords and titles {{{2

echo 'Parsing notes files...'

declare -A blocks
declare -A keywords
declare -A titles

declare file
for file in *.md; do
    _get_yaml_block $file
    declare -a file_keywords=( ${(f)"$(_parse_yaml_block keywords $file)"} )
    declare -Ua all_keywords
    all_keywords+=( $file_keywords )
    keywords[$file]=${(j:++:)file_keywords}
    titles[$file]="$(_parse_yaml_block title $file)"
done

# Write metadata block to file {{{2

echo 'Building index files...'

declare keyword
for keyword in $all_keywords; do
    declare index_file=${keyword// /_}_index.md
    if [[ $keyword[1] == '@' ]]; then
        index_file=${index_file/@/_}
        declare title=${keyword/@/\\@}
    else
        declare title=$keyword
    fi

    # If the index file exists, use its existing metadata block; otherwise, use
    # a default metadata block
    if [[ -f $index_file ]]; then
        declare -a block=( '---' ${(s:++:)blocks[$index_file]} '---'$'\n' )
    else
        declare -a block=(
            '---'
            'id: '${index_file%.md}
            'title: '$title
            'keywords:'
            '---'$'\n'
        )
    fi

    # Before the index file is (re-)created, create a temporary copy of the
    # index file so that we can check later if there were any changes
    if [[ -f $index_file ]]; then
        cp $index_file /tmp/build_index.md
    else
        touch /tmp/build_index.md
    fi
    trap '[[ -f /tmp/build_index.md ]] && rm /tmp/build_index.md' EXIT

    print -lr -- $block > $index_file

    # Write links to files containing `keyword` to file {{{2
    # Create a list of all files that have the keyword `keyword`
    declare -Ua keyword_files=( ${(k)keywords[(R)*$keyword++*]} )
    keyword_files+=( ${(k)keywords[(R)*++$keyword*]} )
    keyword_files+=( ${(k)keywords[(R)$keyword]} )

    # Create a list of all index files that have the keyword `keyword`
    declare -a sub_index_files=( ${keyword_files:#[[:digit:]]*} )

    declare sub_index_file
    for sub_index_file in ${(i)sub_index_files}; do
        title=$titles[$sub_index_file]
        declare link='## ['$title']['$sub_index_file']'
        echo $link
        echo

        # Get the keyword corresponding to the sub-index file
        declare sub_keyword=${${sub_index_file%_index.md}//_/ }
        [[ $sub_keyword[1] == ' ' ]] && sub_keyword=${sub_keyword/ /@}

        # Create a list of all files that have the keyword `sub_keyword`
        declare -Ua sub_keyword_files=( ${(k)keywords[(R)*$sub_keyword++*]} )
        sub_keyword_files+=( ${(k)keywords[(R)*++$sub_keyword*]} )
        sub_keyword_files+=( ${(k)keywords[(R)$sub_keyword]} )

        # Create a list of all files that have both the keyword `keyword` and
        # the keyword `sub_keyword`
        declare -a both_keyword_files=( ${sub_keyword_files:*keyword_files} )
        both_keyword_files=( ${both_keyword_files:|sub_index_files} )

        for file in ${(nO)both_keyword_files}; do
            title=$titles[$file]
            link='['$title']['$file']'
            echo '*  ' $link
        done
        echo

        declare -Ua used_keyword_files
        used_keyword_files+=( $both_keyword_files )
    done >> $index_file

    # Now that the loop is complete, append links to only those files that have
    # not already been linked to
    used_keyword_files+=( $sub_index_files )
    declare -Ua unused_keyword_files=( ${keyword_files:|used_keyword_files} )
    {
        for file in ${(nO)unused_keyword_files}; do
            title=$titles[$file]
            link='| ['$title']['$file']'
            echo $link
        done
        echo
    } >> $index_file

    # We have been using reference-style links, so add references {{{2
    for file in ${(o)keyword_files}; do
        declare ref=${file//_/%20}
        if [[ $ref[1,3] == '%20' ]]; then
            ref="@${ref#\%20}"
        fi
        ref="${ref%.md}.html"
        echo '['$file']:' $ref
    done >> $index_file

    used_keyword_files=()

    # Compare index file to temporary file {{{2
    # If they are different, add the index file to the list of files to be
    # converted to html by Pandoc
    declare html_file="$keyword index.html"
    if [[
        -n "$(diff $index_file /tmp/build_index.md)" || -n $pandoc_always
    ]]; then
        declare -a input files
        input_files+=( $index_file )
        declare -a output_files
        output_files+=( $html_file )
    fi

    # Create a list of the index files creates by this process {{{2
    # To compare to index files actually existing in directory and identify
    # index files that don't correspond to any existing keywords
    declare -a index_files
    index_files+=( $index_file )

    declare -a html_files
    html_files+=( $html_file )

    # Create a file containing a list of keywords for Vim completion {{{2
    echo $keyword
done > index_tmp.txt
sort --ignore-case index_tmp.txt > index.txt
rm index_tmp.txt

# Also convert index files to html {{{2

convert_to_html() {
    echo 'Converting' $1 'to html...'
    pandoc --defaults=notes --output=$2 $1
    echo 'Converted' $1 'to html.'
}

[[ -n $input_files ]] && echo 'Converting index files to html in background...'

for input_file output_file in ${input_files:^output_files}; do
    convert_to_html $input_file $output_file &
done

# Check if there are index files corresponding to unused keywords {{{2

echo 'Checking for index files corresponding to unused keywords...'

declare -a old_md_files=(
    ${(f)"$(comm -13 \
        =(print -l $index_files | sort) \
        =(print -l *index.md | sort))"}
)

declare -a old_html_files=(
    ${(f)"$(comm -13 \
        =(print -l $html_files | sort) \
        =(print -l *index.html | sort))"}
)

declare -a old_files=( $old_md_files $old_html_files )

if [[ -n $old_files ]]; then
    echo 'build-index:' \
        'the following index files do not correspond to any keywords:'
    print -l $old_files
fi >&2

echo 'Done.'
# }}}2

# }}}1
