#!/bin/zsh

# Utility functions for _get_block {{{1

__start_found() { # {{{2
    # Return 1 if the start of a YAML block is found; return 0 if it is not
    # found. Takes the array `start_end_line_numbers` as its argument
    local -i possible_start
    local -i possible_end
    while true; do
        # The start must be a `---` line (as opposed to a `...` line); it must
        # not be followed by a blank line; and, if it is not the first line of
        # the document, it must be preceded by a blank line. Eliminate any
        # lines that do not meet these criteria until a possible start is
        # found.
        while [[
            $file[$1] != ---[[:space:]]# ||
            $file[$1+1] == [[:space:]]# ||
            ($1 -ne 1 && $file[$1-1] != [[:space:]]#)
        ]]; do
            shift

            if (( $# < 2 )); then
                start_end_line_numbers=( $* )
                return 1
            fi
        done

        start_end_line_numbers=( $* )
        local -i possible_start=$start_end_line_numbers[1]
        local -i possible_end=$start_end_line_numbers[2]

        # Check if block is empty
        (( possible_end - possible_start == 1 )) && return 0

        # Check if there is a key
        if [[ -n $key_line_numbers ]] && __key_found $key_line_numbers; then
            start_end_line_numbers=( $* )
            return 0
        else
            shift
            continue
        fi
    done
}

__key_found() { # {{{2
    # Return 0 if a key is found in the possible YAML block; return 1 if one is
    # not found. Takes `key_line_numbers` as its argument
    while (( $1 < possible_start )); do
        shift

        if (( $# == 0 )); then
            key_line_numbers=( $* )
            return 1
        fi
    done

    if (( $1 > possible_start && $1 < possible_end )); then
        key_line_numbers=( $* )
        return 0
    else
        key_line_numbers=( $* )
        return 1
    fi
}
# }}}2

# _get_block() {{{1

setopt rc_quotes

declare key_start_chars='[^\-?:,[\]{}#&*!|>''"%@`[:space:]]'
declare key_can_start_if_chars='[?:\-](?=[^[:space:]])'
declare key_chars="(${key_start_chars}|${key_can_start_if_chars})"
declare key_subsequent_chars='(\S#|:\S|[^[:space:]:#])*'
key_chars="${key_chars}${key_subsequent_chars}"

declare unquoted_key_regex="${key_chars}(\s+${key_chars})*"
declare quoted_key_regex="'([^']|'')*'"
declare dquoted_key_regex='"([^"]|\\")*(?<!\\)"'
declare key_regex="^\s*(?(?=^')${quoted_key_regex}|"
key_regex="${key_regex}(?(?=^\")${dquoted_key_regex}|"
key_regex="${key_regex}${unquoted_key_regex}))\s*:(\s|$)"

_get_block() {
    local -a start_end_line_numbers=(
        ${(f)"$(
            pcre2grep --line-number '^(---|\.\.\.)\s*$' $1 | cut -d : -f 1
        )"}
    )

    if (( $#start_end_line_numbers < 2 )); then
        echo 'build-index: cannot get YAML block for' $1 >&2
        return 1
    fi

    local -a key_line_numbers=(
        ${(f)"$(
            pcre2grep --line-number $key_regex $1 | cut -d : -f 1
        )"}
    )

    local -a file=( "${(@f)$(cat $1)}" )
    local -i block_start
    local -i block_end
    local -a block=()

    while __start_found $start_end_line_numbers; do
        block_start=$start_end_line_numbers[1]
        block_end=$start_end_line_numbers[2]
        if (( block_end - block_start == 1 )); then
            [[ ! -n $got_empty_block ]] && local -i got_empty_block=1
        else
            block+=( $file[block_start+1,block_end-1] )
        fi

        # Remove the starting and ending line numbers from the array
        start_end_line_numbers=( $start_end_line_numbers[3,-1] )
        if [[ ! -n $start_end_line_numbers ]]; then
            break
        fi
    done

    if [[ ! -n $block && ! -n $got_empty_block ]]; then
        echo 'build-index: cannot get YAML block for' $1 >&2
        return 1
    fi

    # Cache the block(s) in the `blocks` associative array
    blocks[$1]=${(j:++:)block}
}

# {{{1 Utility functions for `_parse_block`

# {{{2 __check_quotes()

__check_quotes() {
    if [[ $value[1] == "'" ]]; then
        single_quote=1
    elif [[ $value[1] == '"' ]]; then
        double_quote=1
    elif [[ $value[1] == '[' ]]; then
        bracket=1
    fi

    if [[ $value[-1] == "'" && $value[-2] == [^\'] ]]; then
        single_quote=0
    elif [[ $value[-1] == '"' && $value[-2] == [^\\] ]]; then
        double_quote=0
    elif [[ $value[-1] == "]" ]]; then
        bracket=0
    fi
}

# {{{2 __get_indent()

__get_indent() {
    echo ${#${1}%%[^[:space:]]*}
}

# {{{2 __append_to_array()

__append_to_array() {
    if [[ $value[1] == "'" ]]; then
        single_quote=1
    elif [[ $value[1] == '"' ]]; then
        double_quote=1
    elif [[ $value[1] == '[' ]]; then
        bracket=1
    fi

    values+=( $value )
}

# {{{2 __append_to_string()

__append_to_string() {
    if [[ -n $values[-1] ]]; then
        values[-1]="$values[-1] $value"
    else
        values[-1]=$value
    fi
}

# {{{2 __parse_dashed()

__parse_dashed() {
    trap "trap 'unset list_indent' EXIT" EXIT

    # If `list_indent` has not been defined, then this is the first list item.
    # Therefore, set `list_indent` to whatever the `line_indent` of this line
    # is.
    if [[ ! -n $list_indent ]]; then
        declare -gi list_indent=$line_indent
        value=${value##-[[:space:]]##}

        if [[ $value[1] == '[' ]]; then
            echo 'build-index: for key' $1 'in' $2',' \
                'list not allowed within list' >&2
            return 1
        fi

        __append_to_array

    elif (( line_indent < list_indent )); then
        echo 'build-index: for key' $1 'in' $2',' \
            'line outside scope of list' >&2
        return 1

    elif (( line_indent == list_indent )); then
        value=${value##-[[:space:]]##}

        if [[ $value[1] == '[' ]]; then
            echo 'build-index: for key' $1 'in' $2',' \
                'list not allowed within list' >&2
            return 1
        fi

        __append_to_array

    elif (( line_indent > list_indent )); then
        __append_to_string
    fi
}

# {{{2 __bracket_single_quotes()

__bracket_single_quotes() {
    value=${value//\'\'/++}

    if [[ $value[-1] == "'" ]]; then
        value=${value%\'}
        single_quote=0
    fi

    # Because the starting and ending quotes (if there were any on this line)
    # have been stripped, and because escaped quotes have been substituted with
    # something else, if there are any quotes left, that means either that
    # there's no closing quote or that there's an unescaped quote
    if [[ $value =~ "'" ]]; then
        if (( single_quote == 1 )); then
            echo 'build-index: for key' $1 'in' $2',' \
                'closing quote not found' >&2
            return 1
        elif (( single_quote == 0 )); then
            echo 'build-index: for key' $1 'in' $2',' \
                'unescaped single quotes found' >&2
            return 1
        fi
    fi

    value=${value//++/\'}
}

# {{{2 __bracket_double_quotes()

__bracket_double_quotes() {
    value=${value//\\\"/++}

    if [[ $value[-1] == '"' ]]; then
        value=${value%\"}
        double_quote=0
    fi

    # Because the starting and ending quotes (if there were any on this line)
    # have been stripped, and because escaped quotes have been substituted with
    # something else, if there are any quotes left, that means either that
    # there's no closing quote or that there's an unescaped quote
    if [[ $value =~ '"' ]]; then
        if (( double_quote == 1 )); then
            echo 'build-index: for key' $1 'in' $2',' \
                'closing quote not found' >&2
            return 1
        elif (( double_quote == 0 )); then
            echo 'build-index: for key' $1 'in' $2',' \
                'unescaped double quotes found' >&2
            return 1
        fi
    fi

    value=${value//++/\"}
}

# }}}2

# {{{1 _parse_block()

_parse_block() {
    # {{{2 Get values from first line

    # Find the line in the block with the key we're looking for; disregard
    # any lines before this
    local -a block=( ${(s:+:)blocks[$2]} )
    local -i first_line_num=$block[(I)[[:space:]]#$1:([[:space:]]##*)#]

    if (( $first_line_num > $#block )); then
        echo 'build-index: for key' $1 'in' $2',' \
            'key not found' >&2
        return 1
    fi

    # Get indent of the line containing the key; every subsequent line is going
    # to have to be indented at least as much as this line, unless it's part of
    # a properly quoted or bracketed string
    local -i key_indent=$(__get_indent $block[$first_line_num] )

    # Get value (if there is one) and trim whitespace from both ends
    local value=${block[$first_line_num]##[[:space:]]#${1}:([[:space:]]##)#}
    value=${value%%[[:space:]]#}
    local -a values
    values+=( $value )

    # If there is a value on the first line, do not parse subsequent lines
    # beginning with a '- ' as list items
    if [[ -n $value ]]; then
        local -i no_list=1
    else
        local -i no_list=0
    fi

    local -i single_quote=0
    local -i double_quote=0
    local -i bracket=0

__check_quotes

    # {{{2 Get values from subsequent lines

    local -a block_slice=( $block[(( $first_line_num + 1 )),-1] )
    local -i line_indent
    local line
    for line in $block_slice; do
        line_indent=$(__get_indent $line)

        # Trim whitespace from both ends
        value=${line#[[:space:]](#c$line_indent)}
        value=${value%%[[:space:]]#}

        if [[
            no_list -eq 0 &&
            (single_quote -eq 0) &&
            (double_quote -eq 0) &&
            (bracket -eq 0) &&
            $value[1,2] == '- '
        ]]; then

            if [[ $1 == title ]]; then
                echo 'build-index: for key' $1 'in' $2',' \
                    'list not allowed' >&2
                return 1

            elif (( line_indent < key_indent )); then
                echo 'build-index: for key' $1 'in' $2',' \
                    'line outside scope of key' >&2
                return 1

            elif (( line_indent == key_indent )); then
                __parse_dashed $1 $2 || return 1

            elif (( line_indent > key_indent )); then
                __parse_dashed $1 $2 || return 1
            fi

        else
            # If the string is quoted or bracketed, YAML disregards normal
            # indentation rules and always appends subsequent lines to the
            # current value
            if ((
                single_quote == 1 ||
                double_quote == 1 ||
                bracket == 1
            )); then
                __append_to_string || return 1

            elif (( line_indent < key_indent )); then
                echo 'build-index: for key' $1 'in' $2',' \
                    'line outside scope of key' >&2
                return 1

            elif (( line_indent == key_indent )); then
                # If the line has the same indentation as the key, then it
                # either has to be a list item beginning with '- ', which we've
                # already handled, or another key/value pair (in which case
                # we're done parsing)
                if [[ ! -n ${line##[^[\'][^:]#:*} ]]; then
                    break
                else
                    echo 'build-index: for key' $1 'in' $2',' \
                        'key/value pair expected' >&2
                    return 1
                fi

            elif (( line_indent > key_indent )); then
                if [[ -n $list_indent ]]; then
                    if (( line_indent > list_indent )); then
                        __append_to_string || return 1
                    else
                        echo 'build-index: for key' $1 'in' $2',' \
                            'line outside scope of list' >&2
                        return 1
                    fi

                else
                    __append_to_string || return 1
                    # If ever there is an indented line with a value before
                    # there is a list, no list can follow
                    no_list=1

                fi
            fi
        fi

        __check_quotes
    done

# {{{2 Process values

    single_quote=0
    double_quote=0

    # {{{3 Process values with brackets

    if [[ ${values[1][1]} == '[' ]]; then
        if [[ $1 == 'title' ]]; then
            echo 'build-index: for key' $1 'in' $2',' \
                'list not allowed' >&2
            return 1

        elif [[ ${values[-1][-1]} == ']' ]]; then
            value=${${values#'['}%']'}

        else
            echo 'build-index: for key' $1 'in' $2',' \
                'closing bracket not found' >&2
            return 1
        fi

        values=( ${(s:,:)value} )
        local -i index=1
        for value in $values; do
            value=${${value##[[:space:]]#}%%[[:space:]]#}

            if [[ $value[1,2] == '- ' ]]; then
                echo 'build-index: for key' $1 'in' $2',' \
                    'list not allowed within list' >&2
                return 1

            elif [[ $value[1] == "'" && $single_quote -eq 0 ]]; then
                single_quote=1
                value=${value#\'}
                __bracket_single_quotes $1 $2 || return 1

            elif (( single_quote == 1 )); then
                __bracket_single_quotes $1 $2 || return 1

            elif [[ $value[1] == '"' && $double_quote -eq 0 ]]; then
                double_quote=1
                value=${value#\"}
                __bracket_double_quotes $1 $2 || return 1

            elif (( double_quote == 1 )); then
                __bracket_double_quotes $1 $2 || return 1
            fi

            values[$index]=$value
            index=$(( index + 1 ))
        done

        if (( single_quote == 1 || double_quote == 1 )); then
            echo 'build-index: for key' $1 'in' $2',' \
                'closing quote not found' >&2
            return 1
        fi

    # {{{3 Process values with quotes
    else
        # Given the way that values have been collected, any value that starts
        # with a quote but does not end with a quote is breaking YAML parsing
        # rules. If there is both a starting and ending quote, strip off the
        # outside quotes and substitute properly escaped interior quotes with
        # '++'. Then, if any quotes remain, that means that there are unescaped
        # quotes.
        index=1
        for value in $values; do
            if [[ $value[1] == "'" ]]; then
                if [[ $value[-1] == "'" ]]; then
                    value=${${value#\'}%\'}
                    value=${value//\'\'/++}

                    if [[ $value =~ "'" ]]; then
                        echo 'build-index: for key' $1 'in' $2',' \
                            'unescaped single quotes found' >&2
                        return 1
                    fi

                    value=${value//++/\'}
                    values[$index]=$value

                else
                    echo 'build-index: for key' $1 'in' $2',' \
                        'closing quote not found' >&2
                    return 1
                fi

            elif [[ $value[1] == '"' ]]; then
                value=${value//\\\"/++}
                if [[ $value[-1] == '"' ]]; then
                    value=${${value#\"}%\"}

                    if [[ $value =~ '"' ]]; then
                        echo 'build-index: for key' $1 'in' $2',' \
                            'unescaped double quotes found' >&2
                        return 1
                    fi

                    value=${value//++/\"}
                    values[$index]=$value

                else
                    echo 'build-index: for key' $1 'in' $2',' \
                        'closing quote not found' >&2
                    return 1
                fi

            else
                # If the value isn't quoted, it can't start with a '@' or
                # contain a ':'
                if [[ $value[1] == '@' ]]; then
                    echo 'build-index: for key' $1 'in' $2',' \
                        "value begins with '@'" >&2
                    return 1
                elif [[ $value =~ ':' ]]; then
                    echo 'build-index: for key' $1 'in' $2',' \
                        'value contains nested key/value pair' >&2
                    return 1
                fi
            fi

            index=$(( index + 1 ))
        done
    fi

    # }}}3

    # Split keywords one more time at commas, like Pandoc's YAML parser does
    if [[ $1 == 'keywords' ]]; then
        values=( ${(s:,:)${(j:,:)values}} )
        index=1
        for value in $values; do
            value=${${value##[[:space:]]#}%%[[:space:]]#}
            values[$index]=$value
            index=$(( index + 1 ))
        done
    fi

    print -l -- ${values//[[:space:]]##/ }

# }}}2
}

# {{{1 Build an index file for every keyword found in notes

if [[ $1 == (--pandoc-always|-p) ]]; then
    integer pandoc_always=1
    shift
fi

cd $HOME/Documents/Notes || exit 1

setopt extended_glob

declare -A blocks
declare -a file_keywords
declare -Ua all_keywords
declare -A keywords
declare -A titles

# Cache every file's metadata block, title, and keywords, and create a list of
# all keywords
declare file
for file in *.md; do
    _get_block $file
    file_keywords=( ${(f)"$(_parse_block keywords $file)"} )
    all_keywords+=( $file_keywords )
    keywords[$file]=${(j:++:)file_keywords}
    titles[$file]="$(_parse_block title $file)"
done

declare html_file
declare -a html_files=()
declare index_file
declare -a block
declare title
declare -a index_files
declare -Ua keyword_files
declare -a sub_index_files
declare sub_index_file
declare link
declare sub_keyword
declare -Ua sub_keyword_files
declare -a both_keyword_files
declare -Ua used_keyword_files
declare -Ua unused_keyword_files
declare ref
declare -a input_files=()
declare -a output_files=()

for keyword in $all_keywords; do
    html_file="$keyword index.html"
    html_files+=( $html_file )

    index_file=${keyword// /_}_index.md
    if [[ $keyword[1] == '@' ]]; then
        index_file=${index_file/@/_}
        title=${keyword/@/\\@}
    else
        title=$keyword
    fi

    # If the index file exists, use its existing metadata block; otherwise, use
    # a default metadata block
    if [[ -f $index_file ]]; then
        block=( '---' ${(s:++:)blocks[$index_file]} '---'$'\n' )
    else
        block=(
            '---'
            'id: '${index_file%.md}
            'title: '$title
            'keywords:'
            '---'$'\n'
        )
    fi

    # Before the index file is (re-)created, create a temporary copy of the
    # index file so that we can check later if there were any changes
    if [[ -f $index_file ]]; then
        cp $index_file /tmp/build_index.md
    else
        touch /tmp/build_index.md
    fi
    trap '[[ -f /tmp/build_index.md ]] && rm /tmp/build_index.md' EXIT

    print -lr -- $block > $index_file

    index_files+=( $index_file )

    # Create a list of all files that have the keyword `keyword`
    keyword_files=( ${(k)keywords[(R)*$keyword++*]} )
    keyword_files+=( ${(k)keywords[(R)*++$keyword*]} )
    keyword_files+=( ${(k)keywords[(R)$keyword]} )

    # Create a list of all index files that have the keyword `keyword`
    sub_index_files=( ${keyword_files:#[[:digit:]]*} )

    for sub_index_file in ${(i)sub_index_files}; do
        title=$titles[$sub_index_file]
        link='## ['$title']['$sub_index_file']'
        echo $link
        echo

        # Get the keyword corresponding to the sub-index file
        sub_keyword=${${sub_index_file%_index.md}//_/ }
        [[ $sub_keyword[1] == ' ' ]] && sub_keyword=${sub_keyword/ /@}

        # Create a list of all files that have the keyword `sub_keyword`
        sub_keyword_files=( ${(k)keywords[(R)*$sub_keyword++*]} )
        sub_keyword_files+=( ${(k)keywords[(R)*++$sub_keyword*]} )
        sub_keyword_files+=( ${(k)keywords[(R)$sub_keyword]} )

        # Create a list of all files that have both the keyword `keyword` and
        # the keyword `sub_keyword`
        both_keyword_files=( ${sub_keyword_files:*keyword_files} )
        both_keyword_files=( ${both_keyword_files:|sub_index_files} )

        for file in ${(nO)both_keyword_files}; do
            title=$titles[$file]
            link='['$title']['$file']'
            echo '*  ' $link
        done
        echo

        used_keyword_files+=( $both_keyword_files )
    done >> $index_file

    # Now that the loop is complete, append links to only those files that have
    # not already been linked to
    used_keyword_files+=( $sub_index_files )
    unused_keyword_files=( ${keyword_files:|used_keyword_files} )
    {
        for file in ${(nO)unused_keyword_files}; do
            title=$titles[$file]
            link='| ['$title']['$file']'
            echo $link
        done
        echo
    } >> $index_file

    # We have been using reference-style links, so add references
    for file in ${(o)keyword_files}; do
        ref=${file//_/%20}
        if [[ $ref[1,3] == '%20' ]]; then
            ref="@${ref#\%20}"
        fi
        ref="${ref%.md}.html"
        echo '['$file']:' $ref
    done >> $index_file

    used_keyword_files=()

    # Compare to temporary file; if they are different, add the index file to
    # the list of files to be converted to html by Pandoc
    if [[
        -n "$(diff $index_file /tmp/build_index.md)" || -n $pandoc_always
    ]]; then
        input_files+=( $index_file )
        output_files+=( $html_file )
    fi

    # Create a file containing a list of keywords for Vim completion
    echo $keyword
done > index_tmp.txt
sort index_tmp.txt > index.txt
rm index_tmp.txt

# Also convert index files to html
for input_file output_file in ${input_files:^output_files}; do
    pandoc -d notes -o $output_file $input_file
done

# Check if there are any index files that correspond to keywords that are no
# longer used
declare -a old_md_files=(
    ${(f)"$(comm -13 \
        =(print -l $index_files | sort) \
        =(print -l *index.md | sort))"}
)

declare -a old_html_files=(
    ${(f)"$(comm -13 \
        =(print -l $html_files | sort) \
        =(print -l *index.html | sort))"}
)

declare -a old_files=( $old_md_files $old_html_files )

if [[ -n $old_files ]]; then
    echo 'build-index:' \
        'the following index files do not correspond to any keywords:' >&2
    print -l $old_files >&2
fi

# }}}1
