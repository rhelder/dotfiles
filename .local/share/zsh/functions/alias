# Before aliasing, check whether or not name is already defined as a command,
# function, alias, etc. If not, let `alias` act as normal builtin `alias`.
#
# Include a line in `.zshrc` clearing the `alias_zshrc_aliases` array before
# defining any aliases, so that the `alias` function won't throw an error for
# every alias defined the last time `.zshrc` was sourced. (It is possible to
# have `alias` check e.g. whether or not it is being called for the first time
# in `.zshrc`, and if so to clear `alias_zshrc_aliases`, but leaving out this
# check allows `alias` to run significantly faster).

local flags=()
while getopts 'fgmsrL' flag; do
    case $flag in
        (f)     local force=1;;
        (g)     flags+=( -g );;
        (+g)    flags+=( +g );;
        (m)     local mopt=1; flags+=( -m );;
        (+m)    local mopt=1; flags+=( +m );;
        (s)     flags+=( -s );;
        (+s)    flags+=( +s );;
        (r)     flags+=( -r );;
        (+r)    flags+=( +r );;
        (L)     flags+=( -L );;
        (+L)    flags+=( +L );;
    esac
done
shift $(( OPTIND - 1 ))

# Set `zshrc_line` equal to the line number at which `alias` is being called in
# `.zshrc` (empty if called outside `.zshrc`)
local zshrc_line=$funcfiletrace[(r)$XDG_CONFIG_HOME/zsh/.zshrc*]

# If no argument is provided, the `{+|-}m` flag is present, or the `-f` flag is
# present, run the `alias` builtin like normal
if [[ ! $* || $mopt -eq 1 || $force -eq 1 ]]; then
    builtin alias $flags $*

    # If `alias` was called in `.zshrc`, add arguments to `alias_zshrc_aliases`
    # array, so that new alias names can be checked specifically against alias
    # names already used in `.zshrc`
    if [[ $zshrc_line ]]; then
        alias_zshrc_aliases+=( $* )
    fi

else
    # Otherwise, check to see if there is another command, function, alias,
    # etc. by the same name before defining the alias
    local name
    local result
    for arg in $*; do
        name="${arg%%=*}"
        result=$(whence -v $name)

        # If the name is 'not found', define the alias as normal
        if [[ $result =~ "$name not found" ]]; then
            builtin alias $flags $arg

            # If `alias` was called in `.zshrc`, add argument to
            # `alias_zshrc_aliases` array
            if [[ $zshrc_line ]]; then
                alias_zshrc_aliases+=( $arg )
            fi

        else
            # If the result is an alias, only print an error message and return
            # if an alias by the same name has already been defined in
            # `.zshrc`. If it has, the name will be contained in a value in the
            # array `alias_zshrc_aliases`, so reverse index this array.
            if [[ $result =~ "$name is an alias" ]]; then
                if [[ ${alias_zshrc_aliases[(r)$name=*]} ]]; then
                    echo $name 'already defined' >&2
                    return 1
                else
                    builtin alias $flags $arg

                    # If `alias` was called in `.zshrc`, add argument to
                    # `alias_zshrc_aliases` array
                    if [[ $zshrc_line ]]; then
                        alias_zshrc_aliases+=( $arg )
                    fi
                fi

            else
                # If the result is anything other than an alias, print an error
                # message and return
                echo $name 'already defined' >&2
                return 1
            fi
        fi
    done
fi
