# Before aliasing, check whether or not name is already defined as a command,
# function, alias, etc. If not, let `alias` act as normal builtin `alias`.
#
# To use this function, include the following line in your `.zshrc` before
# autoloading:
#
# declare -gi alias_zshrc_sourced=0

local flags=()
while getopts 'fgmsrL' flag; do
    case $flag in
        (f)     local -i force=1;;
        (g)     flags+=( '-g' );;
        (+g)    flags+=( '+g' );;
        (m)     local -i mopt=1; flags+=( '-m' );;
        (+m)    local -i mopt=1; flags+=( '+m' );;
        (s)     flags+=( '-s' );;
        (+s)    flags+=( '+s' );;
        (r)     flags+=( '-r' );;
        (+r)    flags+=( '+r' );;
        (L)     flags+=( '-L' );;
        (+L)    flags+=( '+L' );;
    esac
done
shift $(( OPTIND - 1 ))

# If no argument is provided, the `{+|-}m` flag is present, or the `-f` flag is
# present, run the `alias` builtin like normal
if [[ ! $* || $mopt -eq 1 || $force -eq 1 ]]; then
    builtin alias $flags $*

else
    # If `alias` is being called in `.zshrc` and `alias_zshrc_sourced` is zero
    # then that means `.zshrc` is being sourced at startup or re-sourced by the
    # user. To prevent an error being thrown for every alias defined in
    # `.zshrc` when `.zshrc` is re-sourced, unalias all aliases in `.zshrc`.
    # Clear the `alias_zshrc_aliases` array so that it can be rebuilt as
    # `.zshrc` is sourced.
    if (( alias_zshrc_sourced == 0 )); then
        if [[ $funcfiletrace[(r)$XDG_CONFIG_HOME/zsh/.zshrc*] ]]; then
            if [[ $alias_zshrc_aliases ]]; then
                unalias $alias_zshrc_aliases
            fi
            declare -ga alias_zshrc_aliases=()
            alias_zshrc_sourced=1
        fi
    fi

    # Check to see if there is another command, function, alias, etc. by the
    # same name before defining the alias
    local name
    for arg in $*; do
        name="${arg%%=*}"
        if [[ $(whence $name) ]]; then
            echo 'alias:' $name 'already defined' >&2
            return 1
        else
            builtin alias $flags $arg
            if [[ $funcfiletrace[(r)$XDG_CONFIG_HOME/zsh/.zshrc*] ]]; then
                alias_zshrc_aliases+=( $name )
            fi
        fi
    done
fi
